plugins {
    id 'java'
}

wrapper {
    gradleVersion = '8.8'
}

repositories {
    mavenCentral()
}

group = 'vlad.zinger'
version = '1.0-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs.add('-parameters')
}

configurations {
    agent {
        canBeResolved = true
        canBeConsumed = true
    }
}

ext {
    appiumVersion = '9.2.3'
    slf4jVersion = '2.0.13'
    log4japiVersion = '2.23.1'
    log4jcoreVersion = '2.23.1'
    allureVersion = '2.27.0'
    aspectJVersion = '1.9.22'
    testngVersion = '7.10.0'
}

dependencies {

    agent "org.aspectj:aspectjweaver:$aspectJVersion"

    implementation(
            "io.appium:java-client:$appiumVersion",
            "org.slf4j:slf4j-simple:$slf4jVersion",
            "org.apache.logging.log4j:log4j-api:$log4japiVersion",
            "org.apache.logging.log4j:log4j-core:$log4jcoreVersion",
    )

    testImplementation "org.testng:testng:$testngVersion"
    testImplementation platform("io.qameta.allure:allure-bom:$allureVersion")
    testImplementation "io.qameta.allure:allure-testng"
    testImplementation "org.slf4j:slf4j-simple:$slf4jVersion"
}

test {
    ignoreFailures = true
    useTestNG()
    jvmArgs = [ "-javaagent:${configurations.agent.singleFile}" ]
}